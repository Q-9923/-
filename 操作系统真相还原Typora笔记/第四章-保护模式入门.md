## 第四章-保护模式入门

##### 背景来源

​	1.Intel8086CPU由于自身设计存在很多缺点，最致命的有俩条：1.仅能寻址1MB内存空间；2.用户程序可以通过自由修改段基地址访问所有内存空间而引出的安全问题。CPU厂商将新开发出的cpu命名为工作在保护模式下——也就是提供了一种保护机制不让程序随意访问所有内存空间，同时CPU的寻址范围也达到4GB。而8086的那种工作模式由于保护模式的出现被命名为实模式。这种具有新的工作模式的CPU叫做IA32（工作在32位坏境下）体系架构CPU。

​	2.由于后来的IA32体系cpu必须兼容8086的那种工作模式，所以IA32体系架构cpu也必须可以运行在实模式下。8086只能运行实模式，它使用【段基址：偏移】寻址方式，所以IA32cpu为了兼容8086开发的程序，也得用【段基址；偏移】这种模式，但是又同时为了能够寻址更大的地址空间以及获得安全性检查，所以采用了将段寄存器提供的值（16位）不再作为段基址，而是作为一个选择子去GDT表中找到对应的表项，然后从表项中得到段基址与进行安全性检查。

​	3.由于IA32体系的cpu有俩种主要的工作模式，，而BIOS加载MBR，MBR，加载loader的时候工作在实模式下，此后为了获得更大的地址空间，就必须从实模式切换到保护模式。模式的切换就意味着寻址方式的切换，所以在由实模式切换时要在内存中初始化GDT表，也叫段描述符。结构如下，每个字符的含义p151。



![1738721656280](C:\Users\nihao\AppData\Roaming\Typora\typora-user-images\1738721656280.png)

上面所说的 1MB 和 4GB 只是个范围，并不是具体的边界值。由于段界限只是个偏移量，是从 0 算起 

的，所以实际的段界限边界值=  

（描述符中段界限+1）*（段界限的粒度大小：4KB 或者 1）-1

​	GDT表中每个表项（8B）实质上是一段内存区域的身份证，一个段描述符只能来描述一个内存段。操作系统的代码段，数据段，栈段自然也就个一个段描述符。需要注意的是，GDT表第0个段描述符不可用。因为选择子忘记设置的话，就会是0（就像我们MBR代码一上来就将段寄存全部初始化为0），就会访问这个段描述符，而如果这个段描述符由内容的话，就会将段基址定位到其他我们并不需要的地方，所以直接让GDT表第0个段描述符不可用，未设置的选择访问这个段描述符cpu就会产生异常并阻止。

​	现在我们开始初始化GDT表，我们需要现在boot.intc中定义一系列段描述符的模块化字段宏，这样方便我们可以结构化凑出段描述符，进而方便定义GDT表，我们在boot.inc中也同时定义了模块化的选择字段，可以方便定义选择子，选择子结构如图：

![1738730063981](C:\Users\nihao\AppData\Roaming\Typora\typora-user-images\1738730063981.png)

##### p161代码loader.S剖析

**1.代码功能**

​	启动保护模式后利用初始化的GDT表中的显存段描述符来对显存寻址后操作以显示字符

2.**实现原理**

​	IA32体系架构为了兼容8086，所以支持实模式，同时用保护模式提供更大的cpu寻址能力与访问安全性检查。刚开启电脑时，工作在实模式下，后面为了使用更好的性能而进入保护模式后，段基址寄存器的值不再为实模式下的段基址，而是作为一个选择子，去GDT表中查询对应的段描述符以获得段基址与进行安全检查，配合偏移来获得真是物理地址

3.**代码逻辑**

​	A.初始化GDT表（定义要用到的段描述符）

​	B.利用BIOS中断打印字符串

​	C.打开保护模式，加载GDT表的基址进入GDTR寄存器

​	D.刷新流水线

​	E.加载显存段选择子，在保护模式下操作显存来显示字符

4.**如何实现**

A.%include"boot.inc";指定vstart=0x900；jpm到loader的可执行代码中去

B.利用已经在boot.inc中写好的模块化段描述字段来拼凑出我们需要的代码段，数据段显存段的段描述符。同时GDT表的第0项都是0，还要预留一些段描述符然后定义显存段的选择子

C.查询BIOS中断打印字符串“2 loader in real”



D.开启保护模式：1、打开A20地址线（8086存在高端内存，这部分内存只在逻辑中存在，物理中并没有回应。由于8086只有20跟地址线，所以访问高端内存会自动丢掉最高位，所以没有问题，当时很多程序员利用这个特性偷懒，但是当cpu多了地址线后，之前的8086的程序就会访问对于8086来说的高端内存。所以为了兼容他们的程序，就用A20地址线控制是否能够访问更多的内存。用in与out指令就能与A20交互）；2、加载GDT表的首地址进入GDTR；3、将CR0寄存器的pe位置为1，意为打开保护模式（mov指令）

E通过远跳转刷新流水线.

F.加载显存段选择子，切换32位编译，然后向显存段内存入数据显示字符“p”

5.**代码**loader.S

%include"boot.inc"
section loader vstart=LOADER_BASE_ADDR
LOADER_STACK_TOP equ LOADER_BASE_ADDR
    jmp loader_start                                ;loader一进来是一对GDT段描述符数据，无法执行所以要跳过


GDT_BASE:                                           ;构建GDT及其内部的描述符
    dd 0x00000000
    dd 0x00000000

CODE_DESC:
    dd 0x0000FFFF
    dd DESC_CODE_HIGH4

DATA_STACK_DESC:
    dd 0x0000FFFF
    dd DESC_DATA_HIGH4

VIDEO_DESC:
    dd 0x80000007                                   ;limit=(0xbffff-0xb8000)/40=0x7
    dd DESC_VIDEO_HIGH4                             ;此时dp1已经改为0

    GDT_SIZE equ $ - GDT_BASE
    GDT_LIMIT equ GDT_SIZE - 1
    times 60 dq 0                                   ;此处预留60个描述符的空间
    SELECTOR_CODE equ (0x0001<<3) + TI_GDT + RPL0   ;相当于(CODE_DESC - GDT_BASE)/8+TI_GDT+RPL0
    SELECTOR_DATA equ (0x0002<<3) + TI_GDT + RPL0   ;同上
    SELECTOR_VIDEO equ (0x0002<<3) + TI_GDT + RPL0  ;同上
gdt_ptr dw GDT_LIMIT                                ;定义加载进入GDTR的数据，前俩字节是gdt界限，后4字节是gdt起始地址
        dd  GDT_BASE
loadermsg db '2 loader in real.'

loader_start:
                                                    ;------------------------------
                                                    ;INT 0x10 功能号0x13  打印字符串
                                                    ;-------------------------------
                                                    ;输入
                                                    ;AH 子功能号=13H
                                                    ;BH=页码
                                                    ;BL=属性(若AL=00H或01H)
                                                    ;cx=字符串长度
                                                    ;(DH、DL)=坐标(行、列)
                                                    ;ES:BP=字符串地址
                                                    ;AL=显示输出方式
                                                    ;0---字符串中只含显示字符，其显示属性在BL中.显示后光标位置不变
                                                    ;1---字符串中只含显示字符，其显示属性在BL中.显示后光标位置改变
                                                    ;2---字符串中含显示字符和显示属性。显示后，光标位置不变
                                                    ;3---字符串中含显示字符和显示属性。显示后，光标位置不变
                                                    ;无返回值
    mov sp,LOADER_BASE_ADDR
    mov bp,loadermsg                                ;ES：BP=字符串地址
    mov cx,17                                       ;cx=字符串长度
    mov ax,0x1301                                   ;AH=13,AL=01h
    mov bx,0x001f                                   ;页号为0(BH=0)蓝底粉红字(BL=1fh)
    mov dx,0x1800                                   ;
    int 0x10                                        ;10h号中断  


                                                    ;----------准备进入保护模式                                                  
                                                    ;1打开A20
                                                    ;2加载GDT
                                                    ;3将cr0的pe位置1



                                                    ;------------打开A20---------
    in al,0x92
    or al,0000_0010B
    out 0x92,al                           
                                                    ;-------------加载GDT--------------
    lgdt[gdt_ptr]



                                                    ;--------------cr0第0位置1---------
    mov eax,cr0
    or eax,0x00000001
    mov cr0,eax
    
                                                    ;jmp dword SELECTOR_CODE：p_mode_start
    jmp SELECTOR_CODE：p_mode_start                 ;刷新流水线 ，避免分支预测的影响，这种cpu优化策略怕jmp跳转
                                                    ;这将导致之前做的预测失效从而起到了刷新作用
[bits 32] 
p_mode_start:
    mov ax,SELECTOR_DATA
    mov ds,ax 
    mov es,ax
    mov ss,ax
    mov esp,LOADER_STACK_TOP
    mov ax,SELECTOR_VIDEO
    mov gs,ax

    mov byte [gs:160],'p'
    
    jmp $                                                                                                                                                                                            
部分解释：第 3 行“LOADER_STACK_TOP equ LOADER_BASE_ADDR”，这个 LOADER_STACK_TOP 是用于 

loader 在保护模式下的栈，它等于 LOADER_BASE_ADDR，其实这是 loader 在实模式下时的栈指针地址。 

只不过进入保护模式后，咱们得为保护模式下的 esp 初始化，所以用了相同的内存地址作为栈顶。 

LOADER_BASE_ADDR 的值是 0x900，这是 loader 被加载到内存中的位置，在此地址之下便是栈

​			全局描述符表 GDT 只是一片内存区域，里面每隔 8 字节便是一个表项，即段描述符。

​			

6.其他代码详解看p162



##### mbr4.S:

                                        ;主引导程序
                                        ;LOADER_BASE_ADDR equ 0xA000
                                        ;LOADER_START_SECTOR equ 0x2
                                        ;-------------------------------
%include"boot.inc"                                      
SECTION MBR vstart=0x7c00
    mov ax,cs
    mov ds,ax
    mov es,ax
    mov ss,ax
    mov fs,ax
    mov sp,0x7c00
    mov ax,0xb800
    mov gs,ax
                                        ;清屏
                                        ;利用0x06号功能上卷全部行，则清屏
                                        ;INT x  功能号0x06 上卷窗口
                                        ;输入
                                        ;AH功能号0x06
                                        ;AL=上卷行数 如果为0表示全部
                                        ;BH=上卷行属性
                                        ;(CL,CH)=窗口左上角的(X,Y)位置
                                        ;(DL,DH)=窗口右下角(X,Y)位置
                                        ;无返回值
    mov ax,0600h
    mov bx,0700h
    mov cx,0                            ;左上角(0,0)
    mov dx,184fh                        ;右下角(80,25)
                                        ;VGA文本模式中一行只能容纳80个字符，共25行
                                        ;下标从0开始，所以0x18=24，0x4f=79
    int 10h                             ;int 10h
                                        ;输出背景色绿色，前景色红色，跳动的字符串"1 MBR"
    mov byte [gs:0x00],'1'   
    mov byte [gs:0x01],0xA4           ;A表示绿色背景色闪烁，4表示前景色为红色
    
    mov byte [gs:0x02],' ' 
    mov byte [gs:0x03],0xA4
    
    mov byte [gs:0x04],'M' 
    mov byte [gs:0x05],0xA4 
    
    mov byte [gs:0x06],'B'
    mov byte [gs:0x07],0xA4
    
    mov byte [gs:0x08],'R'
    mov byte [gs:0x09],0xA4
    
    mov eax,LOADER_START_SECTOR          ;起始扇区1ba地址
    mov bx,LOADER_BASE_ADDR              ;写入的地址
    mov cx,4                             ;带读入的扇区数
    call rd_disk_m_16                    ;以下读程序的起始部分(一个扇区)
    
    jmp LOADER_BASE_ADDR
                                         ;功能：读取硬盘n个扇区
rd_disk_m_16:                            
                                        ;eax=LBA扇区号
                                        ;ebx=将数据写入的内存地址
                                        ;ecx=读入的扇区数
    mov esi,eax                         ;备份eax
    mov di,cx                           ;备份cx
                                        ;读写硬盘
                                        ;先选择特定通道寄存器设置要读取的扇区数
    mov dx,0x1f2                                   
    mov al,cl
    out dx,al                           ;读取的扇区数

    mov eax,esi                         ;恢复ax
                                        ;第二步：在特定通道寄存器中放入要读取扇区的地址，将LBA地址存入0x1f3~0x1f6
                                        ;LBA地址7~0位写入端口0x1f3
    mov dx,0x1f3
    out dx,al
                                        ;LBA地址15~8位写入端口0x1f4
    mov cl,8
    shr eax,cl          ;把eax右移八位,高八位补0,实现中八位变低八位
    mov dx,0x1f4
    out dx,al
                                        ;LBA地址23~16位写入端口0x1f5
    shr eax,cl
    mov dx,0x1f5
    out dx,al
    
    shr eax,cl
    and al,0x0f                         ;lba第24~27位
    or al,0xe0                          ;设置7~4位为1110,表示lba模式
    mov dx,0x1f6
    out dx,al
    
                                        ;第三步向0x1f7端口写入读命令,0x20
    mov dx,0x1f7
    mov al,0x20
    out dx,al
                                        ;第四步,检测硬盘状态
.not_ready:                        
                                        ;同一端口，写时表示写入命令字，读时表示读入硬盘状态
    nop                                 ;nop停滞一下为了等待硬盘是否准备好
    in al,dx
    and al,0x88                         ;第四位为1表示硬盘控制器已准备好数据传输，第七位为1表示硬盘忙
    cmp al,0x08                         
    jnz .not_ready                      ;若未准备好，继续等   

                                        ;第五步，从0x1f0端口读数据
    mov ax,di                           ;di当中存储的是要读取的扇区数
    mov dx,256                          ;每个扇区512字节，一次读取俩个字节所以一个扇区就要读256次，与扇区数相乘就得到总读取次数
    mul dx                              ;8位乘法与16位乘法看书p133，16位乘法会改变dx的值
    mov cx,ax                           ;得到了要读取的总次数然后将这个数字放入cx中
    mov dx,0x1f0
.go_on_read:
    in ax,dx
    mov [bx],ax                         ;没有表明则bx默认配[ds：bx]
    add bx,2
    loop .go_on_read
    ret

    times 510-($-$$) db 0
    db 0x55,0xaa
#### -------------------------boot.S-----------------------

                                                ;-----loader和kernel----
LOADER_BASE_ADDR equ 0x900
LOADER_START_SECTOR equ 0x2

                                                ;模块化的gdt描述符字段宏
DESC_G_4K   equ   1_00000000000000000000000b    ;设置段界限的单位为4KB
DESC_D_32   equ    1_0000000000000000000000b    ;设置代码段/数据段的有效地址（段内偏移地址）及操作数大小为32位 
DESC_L      equ     0_000000000000000000000b    ;64位代码段标记位，现在编写的是32位操作系统，此处标记0
DESC_AVL    equ      0_00000000000000000000b    ;此标志位是为了给操作系统或其他软件设计的一个自定义位
                                                ;可以将这个位用于任何自定义的需求
                                                ;例如，操作系统可以用这个位来标记这个段是否正在被使用或用于其他特定需求
                                                ;这取决于开发者如何使用这个位。但从硬件的角度来看，AVL位没有任何特定的功能或意义，它的使用由软件来决定     
DESC_LIMIT_CODE2    equ 1111_0000000000000000b  ;定义代码段要用的段描述符高32位中16-19段界限为全1
DESC_LIMIT_DATA2    equ DESC_LIMIT_CODE2        ;定义数据段要用的段描述符高32位中16-19段界限为全1  
DESC_LIMIT_VIDEO2   equ 0000_000000000000000b   ;定义我们要操作显存时对应的段描述符的高为32位中16-19段界限为全0
DESC_P      equ         1_000000000000000b      ;定义了段描述符中的P标志位，表示该描述符指向的段是否在内存中
DESC_DPL_0  equ          00_0000000000000b      ;定义DPL为0的字段
DESC_DPL_1  equ          01_0000000000000b      ;定义DPL为1的字段   
DESC_DPL_2  equ          10_0000000000000b      ;定义DPL为2的字段
DESC_DPL_3  equ          11_0000000000000b      ;定义DPL为3的字段                                        
DESC_S_CODE equ            1_000000000000b      ;无论代码段还是数据段，对于cpu来说都是非系统段，所以将S位置为1，见书p153图
DESC_S_DATA equ     DESC_S_CODE                 ;无论代码段还是数据段，对于cpu来说都是非系统段，所以将S位置为1，见书p153图
DESC_S_sys  equ            0_000000000000b      ;将段描述符的S位置为0，表示系统段
DESC_TYPE_CODE  equ         1000_00000000b      ;x=1,c=0,r=0,a=0代码段时可执行的，非依从的，不可读的，已访问位a清零
DESC_TYPE_DATA  equ         0010_00000000b      ;x=0,e=0,w=1,a=0数据段是不可执行的，向上扩展的，可写的，已访问位a清零


                                                ;定义代码段数据段显存段的高32位
DESC_CODE_HIGH4 equ (0x00<<24)+DESC_G_4K+DESC_D_32+DESC_L+DESC_AVL+DESC_LIMIT_CODE2+DESC_P+DESC_DPL_0+DESC_S_CODE+DESC_TYPE_CODE+0x00 
DESC_DATA_HIGH4 equ (0x00<<24)+DESC_G_4K+DESC_D_32+DESC_L+DESC_AVL+DESC_LIMIT_DATA2+DESC_P+DESC_DPL_0+DESC_S_DATA+DESC_TYPE_DATA+0x00
DESC_VIDEO_HIGH4 equ (0x00<<24)+DESC_G_4K+DESC_D_32+DESC_L+DESC_AVL+DESC_LIMIT_VIDEO2+DESC_P+DESC_DPL_0+DESC_S_DATA+DESC_TYPE_DATA+0x0b

                                                ;-----------模块化的选择子字段宏----------
RPL0    equ     00b                             ;定义选择子的RPL为0
RPL1    equ     01b                             ;定义选择子的RPL为1
RPL2    equ     10b                             ;定义选择子的RPL为2
RPL3    equ     11b                             ;定义选择子的RPL为3
TI_GDT     equ      000b                        ;定义段选择子请求的段描述符是在GDT中
TI_LDT      equ     100b                        ;定义段选择子请求的段描述符是在LDT中

指令：

nasm -o /home/qiji-9923/bochs/mbr /home/qiji-9923/bochs/chapter_4/mbr4.S -I /home/qiji-9923/bochs/chapter_4/include/

dd if=/home/qiji-9923/bochs/mbr of=/home/qiji-9923/bochs/hd60M.img bs=512 count=1 conv=notrunc

nasm -o /home/qiji-9923/bochs/loader /home/qiji-9923/bochs/chapter_4/loader4.S -I /home/qiji-9923/bochs/chapter_4/include/

dd if=/home/qiji-9923/bochs/loader of=/home/qiji-9923/bochs/hd60M.img bs=512 count=4 conv=notrunc seek=2
执行：
bin/bochs -f bochsrc.disk

![1738744778082](C:\Users\nihao\AppData\Roaming\Typora\typora-user-images\1738744778082.png)

**左下角的字符串“2 loader in real”是在实模式下用 BIOS 中断 0x10 打印的。左上角第 2 行的** 

**字符'P'，这是咱们在保护模式下输出的。一个程序历经两种模式，各模式下都打印了字符，为了区别实模** 

**式下的打印，所以字符串中含有“inreal”。**

**默认的文本显示模式是** 

**80*25，即每行是 80 个字符（0～79），每个字符占 2 字节，故传入偏移地址是 80*2=160。显存中每个字** 

**符的低字节是字符的 ASCII 值，高字节是属性位。这里咱们没有传入属性值，便会默认为黑底白字。**